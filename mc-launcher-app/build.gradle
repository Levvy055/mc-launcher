buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id "java"
    id "groovy"
    id "application"

    // MacAppBundle
    // https://plugins.gradle.org/plugin/com.github.cr0.macappbundle
    id "com.github.cr0.macappbundle" version "3.1.0"

    // Jre Bundle
    // https://plugins.gradle.org/plugin/net.alchim31.getdown
//    id "net.alchim31.getdown" version "0.4.1"

    // Launch4j plugin
    // https://plugins.gradle.org/plugin/edu.sc.seis.launch4j
    id "edu.sc.seis.launch4j" version "1.6.1"
}

group 'subakstudio'
version '1.4'

sourceCompatibility = 1.7

def appDisplayName = 'McLauncher'
def appPackageName = 'com.subakstudio.mclauncher'

mainClassName = "com.subakstudio.mclauncher.McLauncher"

jar {
    baseName 'mc-launcher'
}

dependencies {
    compile project(':mc-launcher-gui')
    compile project(':mc-launcher-fx')

    // Intellij Form
    compile 'com.intellij:forms_rt:7.0.3'

    // Using sfl4j allows us to replace different loggers later, if desired
    compile 'org.slf4j:slf4j-api:1.7.13'

    // sfl4j is just a facade for loggers. To actually get logging to work, you need to
    // tie it in with some actual implementation, e.g., logback
    def logbackVersion = '1.1.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    compile group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion

    compile 'org.codehaus.groovy:groovy-all:2.3.11'

    // OkHttp for Http Client
    compile 'com.squareup.okhttp:okhttp:2.7.0'

    // Filename util
    compile 'commons-io:commons-io:2.4'

    // Launch4J
//    compile 'gradle.plugin.edu.sc.seis.gradle:launch4j:1.6.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

launch4j {
    mainClassName = project.mainClassName
    bundledJrePath = 'jre'
    bundledJre64Bit = true
    jreRuntimeBits = '64'
    // NOTE: Icon from http://www.rw-designer.com/icon-detail/5548
    icon = '../../../resources/TNT.ico'
}

Properties properties = new Properties()

if (project.file("local.properties").exists()) {
    properties.load(project.file("local.properties").newDataInputStream())
} else {
    println("local.properties file is required.")
}

def releasesDir = properties.getProperty('releases.dir')
def thisReleaseDir = "$releasesDir/$project.version"
def thisReleaseName = project.name + "-" + project.version
def distsDir = "$buildDir/distributions"

task unzipJre(type: Copy) {
    destinationDir = file(project.buildDir)
    def jreZipFile = properties.getProperty('jre.zip.file')
    if (jreZipFile != null) {
        from(zipTree(jreZipFile)) {
            include "**/**"
        }
    }
}

task distWin64WithJRE(dependsOn: ['distZip', 'launch4j', 'unzipJre'], type: Zip) {
    destinationDir file(distsDir)
    archiveName "$thisReleaseName-win64-jre.zip"

    into("jre") {
        from("$buildDir/jre")
    }
    into("lib") {
        from("$buildDir/launch4j/lib")
    }
    into("bin") {
        from("$buildDir/scripts")
    }
    into("") {
        from("$buildDir/launch4j/$project.name" + ".exe")
    }
}

task distWin64WithoutJRE(dependsOn: ['distZip', 'launch4j'], type: Zip) {
    destinationDir file(distsDir)
    archiveName "$thisReleaseName-win64.zip"
    into("lib") {
        from("$buildDir/launch4j/lib")
    }
    into("bin") {
        from("$buildDir/scripts")
    }
    into("") {
        from("$buildDir/launch4j/$project.name" + ".exe")
    }
}

task copyWin64DistsToReleasesDir(type: Copy) {
    destinationDir file(thisReleaseDir)
    from distsDir
    include "*-win64*.zip"
    duplicatesStrategy 'exclude'
}

macAppBundle {
    appName = appDisplayName
//    appCategory = "public.app-category.utilities"
    // Converted from https://iconverticons.com/online/
    icon = "../resources/5548-256x256x8.icns"
//    agent = true

//    version = "0.1.3-alpha+SNAPSHOT.123456789"
//    shortVersion = "0.1.3a (nightly)"

    mainClassName = project.mainClassName

//    bundleJRE = false
    bundleExecutable = appDisplayName
    bundleIdentifier = appPackageName
    bundleCopyright = "Copyright 2015 Subak Studio"

//    bundleExtras.put("NSHighResolutionCapable", "true")

//    javaProperties.put("file.encoding", "utf-8")
//    javaXProperties.add("mx2048M")
//    javaXProperties.add("startOnFirstThread")

//    javaClassPath.add("/Users/\$CURRENT_USER/.config/fooapp/plugins/*")

//    archiveName = "Foo App ${pluginVersion}"
}

//getdown {
////    urlbase = "http://mysite.com/myapp/"
//    mainClassName = mainClassName
////    tmplGetdownTxt = tmplGetdownTxt + '\nallow_offline = true'
////    platforms = [Platform.LINUX_I586, Platform.LINUX_X64, Platform.WINDOWS_I586]
//    platforms = [Platform.WINDOWS_I586]
//}

task copyMacosxDistsToReleasesDir(type: Copy) {
    destinationDir file(thisReleaseDir)
    from distsDir
    include "*.dmg"
    duplicatesStrategy 'exclude'
}

